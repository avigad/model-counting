\section{A Formally Verified Toolchain}
\label{section:formally-verified-toolchain}

We set out to formally verify the system with two goals in mind: first, to ensure that the CRAT framework is mathematically
sound; and second, to implement correct-by-construction proof checking and ring evaluation (the ``Trusted Code'' components of \cref{fig:chain}). These two goals are achieved with a single proof development in the \lean{} programming language~\cite{demoura:cade:2021}. Verification was greatly aided by the Aesop~\cite{23limperg_aesop_white_box_best_first_proof_search_lean} automated tactic. In this section, we briefly describe the functionality we implemented and what we proved about it. More information is provided in \cref{appendix:lean}.

\vspace{1em}\noindent
\textbf{Proof checking.} The goal of a CRAT proof is to construct a POG that is equivalent to the input CNF $\inputformula$. The checker begins by parsing the input formula, initializing the set of active clauses to $\theta \leftarrow \inputformula$, and initializing the POG $P$ to an empty one. It then processes every step of the CRAT proof, either modifying its state by adding/deleting clauses in $\theta$ and adding nodes to $P$, or throwing an exception if a step is incorrect. Afterwards, it carries out the \textsc{final conditions} check of \cref{subsection:semantics}. Throughout the process, we maintain invariants which ensure that $P$ is partitioned and that a successful final check entails the logical equivalence of $\inputformula$ and $\phi_\noder$, where $\noder$ is the final POG root.

The specifications we use to state these invariants are built on a general theory of propositional logic, mirroring \cref{section:logical:foundations}. Following the DIMACS CNF convention, we define the data types \lstinline{Var} of variables being positive natural numbers, \lstinline{ILit} of literals being non-zero integers, and \lstinline{PropForm Var} of propositional formulas. Assignments of truth values are taken to be total functions \lstinline{PropAssignment Var := Var → Bool}. Requiring totality is not a limitation: instead of talking about two equal, partial assignments to a subset $X' \subseteq X$ of variables, we can more conveniently talk about two total assignments that agree on $X'$. We write \lstinline{σ ⊨ φ} when \lstinline{σ : PropAssignment Var} satisfies \lstinline{φ : PropForm Var}.

The invariants refer to the checker state \lstinline{st} with fields \lstinline{st.inputCnf} for $\inputformula$, \lstinline{st.clauseDb} for $\theta$, \lstinline{st.pog} for $P$, \lstinline{st.pogDefsForm} for the POG definitions formula $\bigwedge_{\nodeu\in P}\theta_u$, and \lstinline{st.allVars} for all variables (original and extension) added so far. For any $\nodeu\in P$, \lstinline{st.pog.toPropForm u} computes $\phi_\nodeu$. The first two invariants state that assignments to original variables extend uniquely to extension variables defining the POG nodes. In the formalization, we split this into extension and uniqueness:
\begin{lstlisting}
/-- Any assignment satisfying φ₁ extends to φ₂ while preserving values on X. -/
def extendsOver (X : Set Var) (φ₁ φ₂ : PropForm Var) :=
  ∀ (σ₁ : PropAssignment Var), σ₁ ⊨ φ₁ → ∃ σ₂, σ₁.agreeOn X σ₂ ∧ σ₂ ⊨ φ₂
/-- Assignments satisfying φ are determined on Y by their values on X. -/
def uniqueExt (X Y : Set Var) (φ : PropForm Var) :=
  ∀ (σ₁ σ₂ : PropAssignment Var), σ₁ ⊨ φ → σ₂ ⊨ φ → σ₁.agreeOn X σ₂ →
    σ₁.agreeOn Y σ₂

invariants.extends_pogDefsForm : extendsOver st.inputCnf.vars ⊤ st.pogDefsForm
invariants.uep_pogDefsForm : uniqueExt st.inputCnf.vars st.allVars st.pogDefsForm
\end{lstlisting}
The next one guarantees that the set of solutions over the original variables is preserved:
\begin{lstlisting}
def equivalentOver (X : Set Var) (φ₁ φ₂ : PropForm Var) :=
  extendsOver X φ₁ φ₂ ∧ extendsOver X φ₂ φ₁

invariants.equivInput : equivalentOver st.inputCnf.vars st.inputCnf st.clauseDb
\end{lstlisting}
Finally, for every node $\nodeu\in P$ with corresponding literal $u$ we ensure that $\phi_\nodeu$ is partitioned (\cref{def:partitioned-operation-formula}) and relate $\phi_\nodeu$ to its clausal encoding $\theta_u \doteq u \wedge \bigwedge_{\nodev\in P}\theta_v$:
\begin{lstlisting}
def partitioned : PropForm Var → Prop
  | tr | fls | var _ => True
  | neg φ    => φ.partitioned
  | disj φ ψ => φ.partitioned ∧ ψ.partitioned ∧ ∀ τ, ¬(τ ⊨ φ ∧ τ ⊨ ψ)
  | conj φ ψ => φ.partitioned ∧ ψ.partitioned ∧ φ.vars ∩ ψ.vars = ∅

invariants.partitioned : ∀ u : ILit, (st.pog.toPropForm u).partitioned
invariants.equivalent_lits : ∀ u : ILit, equivalentOver st.inputCnf.vars
    (u ∧ st.pogDefsForm) (st.pog.toPropForm x)
\end{lstlisting}
These are maintained by valid CRAT proofs. Together with a few additional invariants
that ensure the correctness of cached computations, they imply the soundness theorem for $P$ with root
node $\noder$: the \textsc{final conditions} ensure that $\theta$ is logically equivalent to
$\pogformula \doteq \{\{r\}\} \cup \; \bigcup_{\nodeu \in P} \theta_{u}$
(\lstinline{equivalent st.clauseDb (r ∧ st.pogDefsForm)}), and from the invariants we can prove
$\inputformula \iff \phi_\noder$ (\lstinline{equivalent st.inputCnf (st.pog.toPropForm r)}).
After certifying a proof, the checker can pass its in-memory POG representation to the ring evaluator.
% JA: I cut a few words here to save a line.

\vspace{1em}\noindent
\textbf{Ring evaluation}. We formalized the central quantity (\ref{eqn:rep}) in the ring evaluation problem
(\cref{def:ring_evaluation}) in a commutative ring \lstinline{R} as follows:
\begin{lstlisting}
def weightSum {R : Type} [CommRing R]
    (weight : Var → R) (φ : PropForm Var) (s : Finset Var) : R :=
  ∑ τ in models φ s, ∏ x in s, if τ x then weight x else 1 - weight x
\end{lstlisting}
The rules for efficient ring evaluation of partitioned formulas are expressed as:
\begin{lstlisting}
def ringEval (weight : Var → R) : PropForm Var → R
  | tr       => 1
  | fls      => 0
  | var x    => weight x
  | neg φ    => 1 - ringEval weight φ
  | disj φ ψ => ringEval weight φ + ringEval weight ψ
  | conj φ ψ => ringEval weight φ * ringEval weight ψ
\end{lstlisting}
\Cref{prop:ring:eval} is then formalized as follows:
\begin{lstlisting}
theorem ringEval_eq_weightSum (weight : Var → R) {φ : PropForm Var} :
    partitioned φ → ringEval weight φ = weightSum weight φ (vars φ)
\end{lstlisting}
To efficiently compute the ring evaluation of a formula represented by a POG node, we implemented
\lstinline{Pog.ringEval} and then proved that it matches the specification above:
\begin{lstlisting}
theorem ringEval_eq_ringEval (pog : Pog) (weight : Var → R) (x : Var) :
  pog.ringEval weight x = (pog.toPropForm x).ringEval weight
\end{lstlisting}
Applying this to the output of our verified CRAT proof checker, which we know to be partitioned
and equivalent to the input formula $\inputformula$, we obtain a proof that our toolchain computes
the correct ring evaluation of $\inputformula$.

\vspace{1em}\noindent
\textbf{Model counting.} Finally, we established that ring evaluation with the appropriate weights
corresponds to the standard model count. To do so, we defined a function that
% JA: deleted the word "directly" to save a line
carries out an integer calculation of the number of models over a set of variables
of cardinality \lstinline{nVars}:
\begin{lstlisting}
def countModels (nVars : Nat) : PropForm Var → Nat
  | tr       => 2^nVars
  | fls      => 0
  | var _    => 2^(nVars - 1)
  | neg φ    => 2^nVars - countModels nVars φ
  | disj φ ψ => countModels nVars φ + countModels nVars ψ
  | conj φ ψ => countModels nVars φ * countModels nVars ψ / 2^nVars
\end{lstlisting}
We then formally proved that for a partitioned formula whose variables are among a finite set
\lstinline{s}, this computation really does count the number of models over \lstinline{s}:
\begin{lstlisting}
theorem countModels_eq_card_models {φ : PropForm Var} {s : Finset Var} :
  vars φ ⊆ s → partitioned φ → countModels (card s) φ = card (models φ s)
\end{lstlisting}
In particular, taking \lstinline{s} to be exactly the variables of \lstinline{φ},
we have that the number of models of \lstinline{φ} on its variables is
\lstinline{countModels φ (card (vars φ))}.